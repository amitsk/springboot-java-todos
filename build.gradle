import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.google.cloud.tools.jib' version '3.4.2'
    id "com.palantir.git-version" version "3.0.0"
}


ext {
    set('springCloudVersion', "2023.0.1")
    restAssuredVersion = '5.4.0'
    jacksonVersion = '2.16.1'
    junitJupiterVersion = '5.10.2'
    resilience4jVersion = '2.2.0'
    retrofitVersion = '2.11.0'
    reactorAdapterVersion = '2.1.0'
    backstopperVersion = '0.15.1'
    assertJVersion = '3.25.3'
    mockitoVersion = '5.11.0'
    mockWebServerVersion = '4.12.0'
    buildVersion = gitVersion()
}

group = 'com.github.amitsk'
version gitVersion()

def buildNumber = gitVersion()

java {
    sourceCompatibility = '21'
}


repositories {
    mavenCentral()
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation('io.projectreactor.addons:reactor-extra')

    implementation "com.nike.backstopper:backstopper-core:$backstopperVersion"
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // Tracing
    implementation 'io.micrometer:micrometer-tracing-bridge-otel:1.2.5'
    implementation 'io.projectreactor:reactor-core-micrometer'


    // JUnit Jupiter API and TestEngine implementation
    testCompileOnly("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testCompileOnly("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")
    testCompileOnly("io.rest-assured:rest-assured:${restAssuredVersion}")
    testCompileOnly "org.assertj:assertj-core:$assertJVersion"
    testCompileOnly "org.mockito:mockito-core:$mockitoVersion"
    testCompileOnly('org.apiguardian:apiguardian-api:1.0.0')
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServerVersion"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    // To avoid compiler warnings about @API annotations in JUnit code

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    testLogging {
        showStandardStreams = true
    }
    useJUnitPlatform()
}
// https://github.com/GoogleContainerTools/jib/tree/master/jib-gradle-plugin
jib {
  from {
    image = 'public.ecr.aws/amazoncorretto/amazoncorretto:21-al2023'
  }
  to {
    image = rootProject.name
  }
  container {
    mainClass = 'com.github.amitsk.sunrise.SpringBootSunriseApplication'
    ports = ['8080']
  }
}

processResources{
    filter (ReplaceTokens, tokens:[version: buildNumber])
}